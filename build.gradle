plugins {
    id 'java'
}

group 'org.example'
version '1.0-SNAPSHOT'

sourceSets {
    worker {}
    main {
        compileClasspath += sourceSets.worker.output
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'parspice:parspice:1.0'
    workerImplementation 'parspice:parspice:1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

task workerJar(type: Jar, dependsOn: classes) {
    manifest {
        attributes 'Implementation-Title': 'ParSPICE Template Worker Jar'
    }
    archiveBaseName.set("worker")
    archiveVersion.set("")
    from {
        sourceSets.worker.output
    }
    from {
        configurations.workerRuntimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}
build.finalizedBy(workerJar)

test {
    useJUnitPlatform()
}

task run(type: JavaExec, dependsOn: workerJar) {
    description = "Run the main process"
    classpath += sourceSets.main.output
    classpath += sourceSets.worker.output
    classpath += configurations.runtimeClasspath
    main = "Main"
}