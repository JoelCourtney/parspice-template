plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.4.31'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

sourceSets {
    worker
    main {
        compileClasspath += sourceSets.worker.output
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // ParSPICE needs to be included for both source sets.
    implementation 'parspice:parspice:1.0'
    workerImplementation 'parspice:parspice:1.0'

    /*
    Add more dependencies for the worker with:

    workerImplementation 'your:dependency:1.0'
     */

    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit5'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

task workerJar(type: Jar, dependsOn: classes) {
    manifest {
        attributes 'Implementation-Title': 'ParSPICE Template Worker Jar'
    }
    archiveBaseName.set("worker")
    archiveVersion.set("")
    from {
        sourceSets.worker.output
    }
    from {
        configurations.workerRuntimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy DuplicatesStrategy.INCLUDE
}

test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileTestKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

compileWorkerKotlin {
    kotlinOptions.jvmTarget = '1.8'
}

task run(type: JavaExec, dependsOn: workerJar) {
    description = "Run the main process"
    classpath += sourceSets.main.output
    classpath += sourceSets.worker.output
    classpath += configurations.runtimeClasspath
    main = "MainKt"
}